// install

// @function
// bound screen_center = Coord
// @function
// bound screen_width = Int
// @function
// bound screen_height = Int

//def coords = coord_conversion[screen_center, screen_width, screen_height]

// There is currently a bug for this definition in the vectorized evaluator.
// @og
// def display_cell[row in view_r, col in view_c] {
//     cell[c]
//     from c, sc
//     where sc = ^ScreenCoord[row, col]
//       and c = coords:gc[sc]
// }
// Split this out to make the if/else simpler in cell[].
@ondemand
def _cell[c in Coord] = {
    flag(c), "!";
    revealed(c), (string[mine_count[c]] <++ " ");
    game_over and mine(c), "x";
}
@ondemand
def cell[c in Coord] = {
    _cell[c];
    (not exists _cell[c]), ".";
}

def display_cell[row, col] {
    cell[c]
    from c in grid
    where c.x = col and c.y = row
}

def view_r = range[1, screen_height, 1]
def view_c = range[1, screen_width, 1]

def victory { count[flag] = count[correct_flag] = count[mine] }
def game_over = exists(c : test(c) and mine(c))

@outline def count0[R] = count[R] <++ 0
def score = count0[correct_flag] - (count0[flag] - count0[correct_flag])

// -------------------------------------------------------------------------------
// -- Text outputs, which are not used for the actual game, only for debugging. --

def align[d in Int] =
    if 0 <= d < 10 then
        " %d"
    else
        "%d"
    end

def bottom_axis {
    """

        %(string_join["", {c : "--", view_c(c)}])
        %(string_join["", {c : "%(align[c])", view_c(c)}])
    """
}
def display_row = {r : string_join[' ', {
        c :
            display_cell[r, c]
    }]
}

def display_grid {
    concat[
        string_join['\n', { i : {
            concat["%(align[r]) | ", display_row[r]]
            // Put 1,1 on the bottom left, so invert the count
            from r where r = game_height - i + 1
        }}],
        bottom_axis
    ]
}

def display = if game_over then
        "Game Over\nScore: %score\n\n%display_grid"
    else if victory then
        "Victory!\nScore: %score\n\n%display_grid"
    else
        display_grid
    end end
